def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitBranch = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

static def getOrDefault(env, defaultVal) {
    def val = System.getenv(env)
    if (val == null || val.isEmpty()) {
        val = defaultVal
    }
    return val
}

pact {
    publish {
        pactDirectory = 'build/pacts'
        pactBrokerUrl = System.getenv("PactBrokerUrl")
        pactBrokerToken = System.getenv("PactAuthenticationToken")
        tags = [getGitBranch(), 'test', 'pre']
        consumerVersion = getGitHash()
    }
}